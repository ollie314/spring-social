    // Docbook and Javadoc building and uploading tasks
apply plugin: 'base'

description = "Spring Social - Documentation"

task docs {
    dependsOn 'manual:asciidoctor', 'apidocs'
}

project('manual') {
    description = "Spring Social - Reference Manual"
    apply plugin: 'base'
    apply plugin: 'asciidoctor'

    ext.expandPlaceholders = ""

    asciidoctor {
        options = [
          eruby: 'erubis',
          attributes: [
              copycss : '',
              icons : 'font',
              'source-highlighter': 'prettify',
              sectanchors : '',
              toc2: '',
              idprefix: '',
              idseparator: '-',
              doctype: 'book',
              numbered: '',
              'spring-social-version' : project.version,
              'spring-version' : springVersion,
              'spring-security-version' : springSecurityVersion,
              'httpComponentsVersion' : httpComponentsVersion,
              'jacksonVersion' : jacksonVersion,
              revnumber : project.version
          ]
        ]
    }


    ext.spec = copySpec {
        into ('reference/htmlsingle') {
            from(asciidoctor.outputDir)
            exclude 'build', 'Guardfile'
        }
    }
}

task apidocs(type: Javadoc) {
    destinationDir = new File(buildDir, 'apidocs')
    title = "Spring Social $version API"

    source coreModuleProjects.collect { project ->
        project.sourceSets.main.allJava
    }

    classpath = files(coreModuleProjects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}

apidocs.options.outputLevel = org.gradle.external.javadoc.JavadocOutputLevel.QUIET

apidocs.options.links = [
    "http://static.springframework.org/spring/docs/3.2.x/javadoc-api",
    "http://static.springsource.org/spring-ldap/docs/1.3.x/apidocs/",
    "http://download.oracle.com/javase/6/docs/api/"
]

apidocs.options.groups = [
    'Spring Social Core':[
        'org.springframework.social',
        'org.springframework.social.connect',
        'org.springframework.social.connect.jdbc',
        'org.springframework.social.connect.mem',
        'org.springframework.social.connect.support',
        'org.springframework.social.oauth1*',
        'org.springframework.social.oauth2*',
        'org.springframework.social.support*'],
    'Spring Social Config':['org.springframework.social.config*'],
    'Spring Social Security':['org.springframework.social.security*'],
    'Spring Social Web':['org.springframework.social.connect.web*']
]

ext.apiSpec = copySpec {
    into('apidocs') {
        from(apidocs.destinationDir)
    }
}

assemble.dependsOn = [apidocs, 'manual:asciidoctor']

task docsZip(type: Zip) {
    dependsOn docs
    group = 'Distribution'
    baseName = rootProject.name
    classifier = 'docs'
    description = "Builds -${classifier} archive containing api and reference " +
        "for deployment at static.springframework.org/spring-social/site/docs."

    with(project(':docs').apiSpec)
    with(project(':docs:manual').spec)
}

task schemaZip(type: Zip) {
    group = 'Distribution'
    baseName = rootProject.name
    classifier = 'schema'
    description = "Builds -${classifier} archive containing all " +
        "XSDs for deployment at static.springframework.org/schema."

    coreModuleProjects.each { module ->
        def Properties schemas = new Properties();

        module.sourceSets.main.resources.find {
            it.path.endsWith('META-INF/spring.schemas')
        }?.withInputStream { schemas.load(it) }

        for (def key : schemas.keySet()) {
            def shortName = key.replaceAll(/http.*schema.(.*).spring-.*/, '$1')
            assert shortName != key
            File xsdFile = module.sourceSets.main.resources.find {
                it.path.endsWith(schemas.get(key))
            }
            assert xsdFile != null
            into (shortName) {
                from xsdFile.path
            }
        }
    }
}
